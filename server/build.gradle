plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.hoooon22'
version = '1.0.10'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	
	// OAuth2 Client
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	
	// Google Calendar API
	implementation 'com.google.api-client:google-api-client:2.2.0'
	implementation 'com.google.apis:google-api-services-calendar:v3-rev20231123-2.0.0'
	implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
	implementation 'com.google.http-client:google-http-client-gson:1.43.3'
	implementation 'com.google.auth:google-auth-library-oauth2-http:1.20.0'
	
	// .env file support
	implementation 'me.paulschwarz:spring-dotenv:4.0.0'
	
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Load .env file
def envFile = file("${project.rootDir}/../.env")
if (envFile.exists()) {
	envFile.eachLine { line ->
		if (!line.startsWith("#") && line.contains("=")) {
			def (key, value) = line.tokenize("=")
			if (key && value) {
				project.ext[key.trim()] = value.trim()
			}
		}
	}
}

processResources {
	filesMatching('application.properties') {
		expand(project.properties)
	}
}
